plugins {
  id 'groovy'
  id 'application'
  id 'edu.sc.seis.launch4j' version '2.3.0'
}

ext {
  mainClass = 'com.github.arminha.jenkinstray.Main'
}
mainClassName = mainClass

repositories {
   mavenCentral()
}

dependencies {
  implementation 'org.codehaus.groovy:groovy:2.4.11'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.8.1'
  implementation 'org.apache.httpcomponents:httpclient:4.5.3'
  implementation 'com.moandjiezana.toml:toml4j:0.7.1'
  testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

String windowsClasspath(FileCollection jarFiles) {
  def cp = ''
  jarFiles.each {
    cp += ";lib\\$it.name"
  }
  return cp.substring(1)
}

launch4j {
  mainClassName = mainClass
  dontWrapJar = true
  jvmOptions = ['-classpath ' + windowsClasspath(sourceSets.main.runtimeClasspath)]
}

distributions {
  main {
    contents {
      from('build/launch4j') {
        include '*.exe'
      }
    }
  }
}
distZip.dependsOn createExe

task wrapper(type: Wrapper) {
  gradleVersion = '4.0'
}
