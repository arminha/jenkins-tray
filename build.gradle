plugins {
  id 'groovy'
  id 'application'
  id 'org.jetbrains.kotlin.jvm' version '1.1.3'
  id 'edu.sc.seis.launch4j' version '2.4.2'
}

ext {
  mainClass = 'com.github.arminha.jenkinstray.Main'
  jvmOpts = ['-Xms10m', '-Xmx32m', '-Xss228k', '-XX:+UseSerialGC']
}
mainClassName = mainClass
applicationDefaultJvmArgs = jvmOpts

repositories {
   mavenCentral()
}

dependencies {
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.9'
  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.8.9'
  implementation 'org.apache.httpcomponents:httpclient:4.5.3'
  implementation 'com.moandjiezana.toml:toml4j:0.7.1'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jre8'
  implementation 'org.jetbrains.kotlin:kotlin-reflect'
  testImplementation 'org.codehaus.groovy:groovy:2.4.12'
  testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

launch4j {
  mainClassName = mainClass
  dontWrapJar = true
  // add dependencies to classpath
  copyConfigurable = sourceSets.main.runtimeClasspath.filter { it.name.endsWith('.jar') }
  jvmOptions = jvmOpts
}

distributions {
  main {
    contents {
      from(tasks.createExe.outputs) {
        include '*.exe'
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.3.1'
}
