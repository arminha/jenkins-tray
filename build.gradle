plugins {
  id 'groovy'
  id 'application'
  id 'org.jetbrains.kotlin.jvm' version '1.3.41'
  id 'edu.sc.seis.launch4j' version '2.4.6'
}

ext {
  mainClass = 'com.github.arminha.jenkinstray.Main'
  jvmOpts = ['-Xms10m', '-Xmx32m', '-Xss228k', '-XX:+UseSerialGC']
}
mainClassName = mainClass
applicationDefaultJvmArgs = jvmOpts

repositories {
   mavenCentral()
}

dependencies {
  def jackson = '2.9.9'
  implementation "com.fasterxml.jackson.core:jackson-databind:$jackson"
  implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson"
  implementation 'com.squareup.okhttp3:okhttp:3.11.0'
  implementation 'com.moandjiezana.toml:toml4j:0.7.2'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation 'org.jetbrains.kotlin:kotlin-reflect'
  testImplementation 'org.codehaus.groovy:groovy:2.5.7'
  testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

launch4j {
  mainClassName = mainClass
  dontWrapJar = true
  // add dependencies to classpath
  copyConfigurable = sourceSets.main.runtimeClasspath.filter { it.name.endsWith('.jar') }
  jvmOptions = jvmOpts
}

distributions {
  main {
    contents {
      from(tasks.createExe.outputs) {
        include '*.exe'
      }
    }
  }
}

wrapper {
  gradleVersion = '5.5.1'
}
